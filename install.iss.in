
[Setup]
AppName=LibUSB-Win32
AppVerName=LibUSB-Win32-@VERSION@
AppId=LibUSB-Win32
AppPublisher=LibUSB-Win32 
AppPublisherURL=http://libusb-win32.sourceforge.net/ 
AppVersion=@VERSION@
DefaultDirName={pf}\LibUSB-Win32-@VERSION@
DefaultGroupName=LibUSB-Win32
LicenseFile=installer_license.txt
Compression=lzma
SolidCompression=yes
MinVersion=4,5
PrivilegesRequired=admin

[Files]
Source: "*.sys"; DestDir: "{win}\system32\drivers"; Flags: uninsneveruninstall replacesameversion restartreplace
Source: "*.dll"; DestDir: "{win}\system32"; Flags: uninsneveruninstall replacesameversion restartreplace

Source: "@BIN_DIST_DIR@\lib\*.*"; DestDir: "{app}\lib"; Flags: recursesubdirs
Source: "@BIN_DIST_DIR@\include\*.h"; DestDir: "{app}\include"; Flags: recursesubdirs

Source: "testlibusb.exe"; DestDir: "{app}\bin";
Source: "testlibusb-win.exe"; DestDir: "{app}\bin";
Source: "testlibusb-win.exe.manifest"; DestDir: "{app}\bin";
Source: "libusbd-nt.exe"; DestDir: "{win}\system32"; Flags: replacesameversion restartreplace
Source: "libusbd-9x.exe"; DestDir: "{win}\system32"; Flags: replacesameversion restartreplace

Source: "@BIN_DIST_DIR@\*.*"; DestDir: "{app}"
Source: "@SRC_DIST_DIR@\*.*"; DestDir: "{app}\src"; Flags: recursesubdirs


[Icons] 
Name: "{group}\Test Program"; Filename: "{app}\bin\testlibusb-win.exe"; 
Name: "{group}\Uninstall LibUsb-Win32"; Filename: "{uninstallexe}"
Name: "{group}\GPL License"; Filename: "{app}\COPYING_GPL.txt"
Name: "{group}\LGPL License"; Filename: "{app}\COPYING_GPL.txt"


[Run]
Filename: "rundll32"; Parameters: "libusb0.dll,usb_install_service_np_rundll"; StatusMsg: "Creating services (this may take a few seconds) ..." 
Filename: "{app}\bin\testlibusb-win.exe"; Description: "Run test application"; Flags: postinstall


[UninstallRun]
Filename: "rundll32"; Parameters: "libusb0.dll,usb_uninstall_service_np_rundll"

[Messages]
StatusUninstalling=Uninstalling %1 (this may take a few seconds) ...


[Code]
function InitializeSetup(): Boolean;
  begin 
  if FileExists(GetWinDir() + '\system32\libusbd-nt.exe') 
    or FileExists(GetWinDir() + '\system32\libusbd-9x.exe') then begin
      MsgBox('An old version of LibUSB-Win32 is already installed on this system. Please uninstall this version first!', 
              mbError, MB_OK);
      Result := FALSE;
  end
  else begin
      Result := TRUE;
  end;
end;