; libusb-win32 filter driver installer script, 
; requires NSIS installer (http://www.nullsoft.com)

!include "MUI.nsh"

!define PRODUCT "LibUsb-Win32"
!define VERSION "@VERSION@"
!define UNINSTALL_PATH "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT}-${VERSION}"


Name "LibUsb-Win32"
Caption "LibUsb-Win32 Version ${VERSION} Setup"


OutFile "libusb-win32-filter-bin-${VERSION}.exe"
InstallDir $PROGRAMFILES\LibUsb-Win32-${VERSION}
SetCompressor lzma

!define MUI_ABORTWARNING
!define MUI_WELCOMEPAGE_TEXT "This wizard will guide you through the installation of ${PRODUCT}.\n\r\n\rPlease close all applications which use USB devices before installing ${PRODUCT}!\n\r\n\rDuring the installation process all USB devices will become temporaryly unavailable!"
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE ".\license_nsis.txt"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_LINK "Visit the LibUsb-Win32 website"
!define MUI_FINISHPAGE_LINK_LOCATION "http://libusb-win32.sourceforge.net"
!define MUI_FINISHPAGE_RUN_TEXT "Run test program"
!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\testlibusb-win.exe"
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_LANGUAGE "English"

ShowInstDetails show
ShowUnInstDetails show


Section "Driver (required)" sec_driver
	SetShellVarContext all
	
	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
   	StrCmp $R0 "" install_start 0

	ClearErrors
	UserInfo::GetName
	Pop $0
	UserInfo::GetAccountType
	Pop $1
	StrCmp $1 "Admin" install_start 0
	MessageBox MB_OK "Administrator access rights are required to install ${PRODUCT}!"
	Quit

install_start:
	ReadRegStr $R0 HKLM "${UNINSTALL_PATH}" "DisplayName"
	ReadRegStr $R1 HKLM "${UNINSTALL_PATH}" "Version"
	StrCmp $R0 "" +3 0
   	MessageBox MB_OK "${PRODUCT} Version $R1 is installed on this system. $\nPlease uninstall first!"
	Quit

	SetOutPath $INSTDIR
  	File "*.txt"
  	SetOutPath $INSTDIR\bin
  	File "testlibusb.exe"
	File "testlibusb-win.exe"
	File "testlibusb-win.exe.manifest"
	File "libusb-nsis.exe"

  	SetOutPath $SYSDIR
  	File "libusb0.dll"
  	SetOutPath $WINDIR\system32\drivers
  	File "libusbfl.sys"

   	DeleteRegKey HKLM "Software\LibUsb-Win32"

	CreateDirectory $SMPROGRAMS\${PRODUCT}

  	CreateShortCut "$SMPROGRAMS\${PRODUCT}\Test Program.lnk" "$INSTDIR\bin\testlibusb-win.exe"
  	CreateShortCut "$SMPROGRAMS\${PRODUCT}\GPL License.lnk" "$INSTDIR\COPYING_GPL.txt"
  	CreateShortCut "$SMPROGRAMS\${PRODUCT}\LGPL License.lnk" "$INSTDIR\COPYING_LGPL.txt"


   	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
   	StrCmp $R0 "" win9x winnt

  winnt:

	DeleteRegValue HKLM "System\CurrentControlSet\Control\Class\{36FC9E60-C465-11CF-8056-444553540000}" "LowerFilters"

	SetOutPath $SYSDIR
	File "libusbd-nt.exe"
	SetOutPath $INSTDIR\bin

	DetailPrint "Stopping kernel service (this can take several seconds) ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --stop_kernel_service_full"

	DetailPrint "Creating kernel service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --create_kernel_service"

	DetailPrint "Restarting USB host controllers (this can take several seconds) ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --restart_host_controllers"

	DetailPrint "Creating system service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --create_system_service"  

	DetailPrint "Starting system service (this can take several seconds) ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --start_system_service"
	
	Goto done
  win9x:
   	DeleteRegValue HKLM "System\CurrentControlSet\Services\Class\USB" "LowerFilters"
	SetOutPath $SYSDIR
	File "libusbd-9x.exe"

	DetailPrint "Stopping kernel service (this can take several seconds) ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --stop_kernel_service_full"

	DetailPrint "Restarting USB host controllers (this can take several seconds) ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --restart_host_controllers"

	DetailPrint "Creating system service ..."
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\RunServices" "LibUsb-Win32 Daemon, Version ${VERSION}" "$SYSDIR\libusbd-9x.exe" 

	DetailPrint "Starting system service (this can take several seconds) ..."
	Exec "libusbd-9x.exe"

  done:

  	WriteUninstaller $INSTDIR\uninstall.exe
 	WriteRegExpandStr HKLM "${UNINSTALL_PATH}" "UninstallString" "$INSTDIR\uninstall.exe"
 	WriteRegExpandStr HKLM "${UNINSTALL_PATH}" "InstallLocation" "$INSTDIR"
  	WriteRegStr HKLM "${UNINSTALL_PATH}" "DisplayName" "${PRODUCT}-${VERSION}"
  	WriteRegStr HKLM "${UNINSTALL_PATH}" "Version" "${VERSION}"
  	WriteRegDWORD HKLM "${UNINSTALL_PATH}" "NoModify" "1"
  	WriteRegDWORD HKLM "${UNINSTALL_PATH}" "NoRepair" "1"

SectionEnd


Section "SDK (optional)" sec_sdk

  	SetOutPath $INSTDIR
  	File /r "libusb-win32-bin-${VERSION}\lib"	
  	File /r "libusb-win32-bin-${VERSION}\include"	

SectionEnd


Section "Source Code (optional)" sec_source

  	SetOutPath $INSTDIR\src
  	File /r "libusb-win32-src-${VERSION}\*"

SectionEnd

Section "Uninstall"

	SetShellVarContext all
   	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
   	StrCmp $R0 "" win9x winnt

  winnt:
	SetOutPath $INSTDIR\bin

	DetailPrint "Stopping system service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --stop_system_service"

	DetailPrint "Deleting system service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --delete_system_service"

	DetailPrint "Stopping kernel service (this can take several seconds) ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --stop_kernel_service"

	Delete $SYSDIR\libusbd-nt.exe

	Goto done
  win9x:
	DeleteRegValue HKLM "Software\Microsoft\Windows\CurrentVERSION\RunServices" "LibUsb-Win32 - Daemon, Version ${VERSION}"

	SetOutPath $INSTDIR\bin

	DetailPrint "Stopping system service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --stop_system_service"

	DetailPrint "Stopping kernel service (this can take several seconds) ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusb-nsis.exe --stop_kernel_service"

	Delete $SYSDIR\libusbd-9x.exe
done:
	RMDir /r $SMPROGRAMS\${PRODUCT}

  	DeleteRegKey HKLM "${UNINSTALL_PATH}"

  	RMDir /r $INSTDIR

SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
!insertmacro MUI_DESCRIPTION_TEXT ${sec_driver} "The driver and Dll"
!insertmacro MUI_DESCRIPTION_TEXT ${sec_sdk} "The Software Development Kit"
!insertmacro MUI_DESCRIPTION_TEXT ${sec_source} "The full source code of all components"
!insertmacro MUI_FUNCTION_DESCRIPTION_END