; libusb-win32 filter driver installer script, 
; requires NSIS installer (http://www.nullsoft.com)

!include "MUI.nsh"

!define PRODUCT "LibUsb-Win32"

!define VERSION "@VERSION@"
!define BIN_DIST_DIR "@BIN_DIST_DIR@"
!define SRC_DIST_DIR "@SRC_DIST_DIR@"

!define UNINSTALL_PATH "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT}"


Name "LibUsb-Win32"
Caption "LibUsb-Win32 Version ${VERSION} Setup"


OutFile "@INSTALLER_TARGET@"
InstallDir $PROGRAMFILES\LibUsb-Win32-${VERSION}
SetCompressor lzma

!define MUI_ABORTWARNING
!define MUI_WELCOMEPAGE_TEXT "This wizard will guide you through the installation of ${PRODUCT}.\n\r\n\rPlease close all applications that use USB devices before installing ${PRODUCT}!\n\r\n\rDuring the installation process all USB devices will become temporaryly unavailable!"
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE ".\license_nsis.txt"
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_LINK "Visit the LibUsb-Win32 website"
!define MUI_FINISHPAGE_LINK_LOCATION "http://libusb-win32.sourceforge.net"
!define MUI_FINISHPAGE_RUN_TEXT "Run test program"
!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\testlibusb-win.exe"
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_LANGUAGE "English"

ShowInstDetails show
ShowUnInstDetails show


Section "Driver (required)" sec_driver
	SetShellVarContext all
	
	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
   	StrCmp $R0 "" install_start 0

	ClearErrors
	UserInfo::GetName
	Pop $0
	UserInfo::GetAccountType
	Pop $1
	StrCmp $1 "Admin" install_start 0
	MessageBox MB_OK "Administrator access rights are required to install ${PRODUCT}!"
	Quit

install_start:
	ReadRegStr $R0 HKLM "${UNINSTALL_PATH}" "DisplayName"
	ReadRegStr $R1 HKLM "${UNINSTALL_PATH}" "Version"
	StrCmp $R0 "" +3 0
   	MessageBox MB_OK "${PRODUCT} Version $R1 is installed on this system. $\nPlease uninstall first!"
	Quit

	SetOutPath $INSTDIR
  	File "*.txt"
  	SetOutPath $INSTDIR\bin
  	File "testlibusb.exe"
	File "testlibusb-win.exe"
	File "testlibusb-win.exe.manifest"
	File "libusbis.exe"

  	SetOutPath $SYSDIR
  	File "libusb0.dll"
  	SetOutPath $WINDIR\system32\drivers
  	File "libusbfl.sys"

   	DeleteRegKey HKLM "Software\LibUsb-Win32"

	CreateDirectory $SMPROGRAMS\${PRODUCT}

  	CreateShortCut "$SMPROGRAMS\${PRODUCT}\Test Program.lnk" "$INSTDIR\bin\testlibusb-win.exe"
  	CreateShortCut "$SMPROGRAMS\${PRODUCT}\GPL License.lnk" "$INSTDIR\COPYING_GPL.txt"
  	CreateShortCut "$SMPROGRAMS\${PRODUCT}\LGPL License.lnk" "$INSTDIR\COPYING_LGPL.txt"


   	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
   	StrCmp $R0 "" win9x winnt

  winnt:

	SetOutPath $SYSDIR
	File "libusbd-nt.exe"
	SetOutPath $INSTDIR\bin

	ReadRegStr $R0 HKLM "System\CurrentControlSet\Control\Class\{36FC9E60-C465-11CF-8056-444553540000}" "LowerFilters"
   	StrCmp $R0 "" +3 0
	SetRebootFlag true
	DeleteRegValue HKLM "System\CurrentControlSet\Control\Class\{36FC9E60-C465-11CF-8056-444553540000}" "LowerFilters"

	DetailPrint "Stopping kernel service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --stop-kernel-service-full"

	DetailPrint "Creating kernel service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --create-kernel-service"

	DetailPrint "Creating system service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --create-system-service"  

	DetailPrint "Starting system service (this can take several seconds) ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --start-system-service"

	Goto done

  win9x:
	SetOutPath $SYSDIR
	File "libusbd-9x.exe"

	ReadRegStr $R0 HKLM "System\CurrentControlSet\Services\Class\USB" "LowerFilters"
   	StrCmp $R0 "" +3 0
	SetRebootFlag true
	DeleteRegValue HKLM "System\CurrentControlSet\Services\Class\USB" "LowerFilters"

	DetailPrint "Stopping kernel service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --stop-kernel-service-full"

	DetailPrint "Creating system service ..."
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\RunServices" "LibUsb-Win32 Daemon, Version ${VERSION}" "$SYSDIR\libusbd-9x.exe" 

	DetailPrint "Starting system service (this can take several seconds) ..."
	Exec "libusbd-9x.exe"

  done:

  	WriteUninstaller $INSTDIR\uninstall.exe
 	WriteRegExpandStr HKLM "${UNINSTALL_PATH}" "UninstallString" "$INSTDIR\uninstall.exe"
 	WriteRegExpandStr HKLM "${UNINSTALL_PATH}" "InstallLocation" "$INSTDIR"
  	WriteRegStr HKLM "${UNINSTALL_PATH}" "DisplayName" "${PRODUCT}-${VERSION}"
  	WriteRegStr HKLM "${UNINSTALL_PATH}" "Version" "${VERSION}"
  	WriteRegDWORD HKLM "${UNINSTALL_PATH}" "NoModify" "1"
  	WriteRegDWORD HKLM "${UNINSTALL_PATH}" "NoRepair" "1"

SectionEnd


Section "SDK (optional)" sec_sdk

  	SetOutPath $INSTDIR
  	File /r "${BIN_DIST_DIR}\lib"	
  	File /r "${BIN_DIST_DIR}\include"	

SectionEnd


Section "Source Code (optional)" sec_source

  	SetOutPath $INSTDIR\src
  	File /r "${SRC_DIST_DIR}\*"

SectionEnd

Section "Uninstall"

	SetShellVarContext all
   	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
   	StrCmp $R0 "" win9x winnt

  winnt:
	SetOutPath $INSTDIR\bin

	DetailPrint "Stopping system service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --stop-system-service"

	DetailPrint "Deleting system service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --delete-system-service"

	DetailPrint "Stopping kernel service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --stop-kernel-service"

	Delete $SYSDIR\libusbd-nt.exe

	Goto done
  win9x:

	SetOutPath $INSTDIR\bin

	DetailPrint "Stopping system service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --stop-system-service"

	DetailPrint "Deleting system service ..."
	DeleteRegValue HKLM "Software\Microsoft\Windows\CurrentVersion\RunServices" "LibUsb-Win32 - Daemon, Version ${VERSION}"

	DetailPrint "Stopping kernel service ..."
	nsExec::ExecToLog "$INSTDIR\bin\libusbis.exe --stop-kernel-service"

	Delete $SYSDIR\libusbd-9x.exe
done:
	RMDir /r $SMPROGRAMS\${PRODUCT}

  	DeleteRegKey HKLM "${UNINSTALL_PATH}"

  	RMDir /r $INSTDIR

SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
!insertmacro MUI_DESCRIPTION_TEXT ${sec_driver} "Driver and Dll"
!insertmacro MUI_DESCRIPTION_TEXT ${sec_sdk} "Software Development Kit"
!insertmacro MUI_DESCRIPTION_TEXT ${sec_source} "Full source code of all components"
!insertmacro MUI_FUNCTION_DESCRIPTION_END